/*
 * Check usage information at: http://hexagonkt.com/gradle/#dokka
 */

apply(plugin: "kotlin")
apply(plugin: "maven-publish")
apply(plugin: "org.jetbrains.dokka")

tasks.register("dokkaJar", Jar) {
    group = "distribution"
    description = "Create a jar file with the source code documentation in Javadoc format."
    dependsOn("dokkaJavadoc")

    archiveClassifier.set("javadoc")
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(fileTree("$buildDir/dokka/javadoc"))
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(dokkaJar)
        }
    }
}

setUpDokka(dokkaHtmlPartial)
setUpDokka(dokkaJavadoc)

private void setUpDokka(final Object dokkaTask) {
    dokkaTask.dependsOn("compileJava", "compileKotlin", "processResources", "processTestResources")
    dokkaTask.moduleName.set(project.name)
    dokkaTask.offlineMode.set(true)
    dokkaTask.dokkaSourceSets {
        configureEach {
            includeNonPublic.set(false)
            reportUndocumented.set(false)
            includes.from(fileTree(projectDir) { include("*.md") })
            samples.from(fileTree(projectDir) { include("**/*Test.kt") })
        }
    }
}

dependencies {
    final String mermaidDokkaVersion = findProperty("mermaidDokkaVersion") ?: "0.4.4"

    dokkaHtmlPartialPlugin("com.glureau:html-mermaid-dokka-plugin:$mermaidDokkaVersion")
}
