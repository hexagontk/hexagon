
apply(plugin: "org.graalvm.buildtools.native")

graalvmNative {
    agent {
        defaultMode = "standard"

        metadataCopy {
            inputTaskNames.add("test")
            final String buildPath = project.buildDir.absolutePath
            outputDirectories.add("$buildPath/resources/main/META-INF/native-image")
        }
    }

    metadataRepository {
        enabled.set(true)
    }

    binaries {
        test {
            quickBuild = true
            runtimeArgs = [ "--xml-output-dir", "build/reports/tests/native" ]
            jvmArgs = [ "-Xmx6g" ]
        }
    }
}

tasks.register("upx", Exec) {
    group = "distribution"
    description = "Compress the native executable using 'upx'."
    dependsOn("nativeCompile")

    final String source = "$buildDir/native/nativeCompile/${project.name}"
    final String target = "$buildDir/native/${project.name}"
    final String os = System.getProperty("os.name").toLowerCase()
    final String command =
        os.contains("windows")? "upx ${source}.exe -o ${target}.exe" : "upx $source -o $target"
    commandLine(command.split(" "))

    doFirst {
        file(target).delete()
    }
}

tasks.register("zipNative", Zip) {
    group = "distribution"
    description = "Compress native executable in a ZIP file."
    dependsOn("nativeCompile")

    final String os = System.getProperty("os.name").toLowerCase()
    final String arch = System.getProperty("os.arch").toLowerCase()
    final String source = os.contains("windows")? "${project.name}.exe" : project.name
    from("$buildDir/native/nativeCompile")
    include(source)
    archiveFileName.set("${project.name}-${project.version}-${os}-${arch}.zip")
    destinationDirectory.set(buildDir.toPath().resolve("distributions").toFile())
}
