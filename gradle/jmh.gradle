/*
 * Check usage information at: http://hexagontk.com/gradle/#jmh
 */

apply(plugin: "me.champeau.jmh")

tasks.compileJmhKotlin.compilerOptions.jvmTarget = tasks.compileKotlin.compilerOptions.jvmTarget
tasks.compileJmhKotlin.compilerOptions.apiVersion = tasks.compileKotlin.compilerOptions.apiVersion

sourceSets {
    jmh {
        kotlin {
            srcDir project.file("main").exists()
                ? project.file("jmh")
                : project.file("src/jmh/kotlin")
        }
    }
}

jmh {
    final String jhmVersion = findProperty("jhmVersion") ?: "1.37"

    jmhVersion.set(jhmVersion)
    benchmarkMode.set(["thrpt"])

    iterations.set(10)
    batchSize.set(1)
    fork.set(1)
    operationsPerInvocation.set(5)
    timeOnIteration.set("1s")

    warmup.set("1s")
    warmupBatchSize.set(5)
    warmupIterations.set(1)
}
