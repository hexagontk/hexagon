
apply from: "../gradle/kotlin.gradle"
apply from: "../gradle/service.gradle"

apply plugin: 'war'
apply plugin: 'me.champeau.gradle.jmh'

mainClassName = "co.there4.hexagon.BenchmarkKt"
applicationDefaultJvmArgs = [
    '-Xms2G', '-Xmx2G', '-XX:+UseNUMA', '-XX:+UseParallelGC', '-XX:+AggressiveOpts'
]

ext.hikariVersion = '2.6.1'
ext.postgresqlVersion = '42.0.0'
ext.mongodbVersion = '3.4.2'
ext.jettyVersion = '9.4.6.v20170531'

ext.ahcVersion = '2.0.31'

ext.jmhBenchmarkVersion = '1.19'

ext.tfbBuildDir = project.buildDir.absolutePath + '/tfb'
ext.databases = [ 'MongoDB', 'PostgreSQL' ]
ext.servers = [ 'Jetty', 'Resin' ]
ext.tests = [servers, databases].combinations ()

war {
    archiveName = "ROOT.war"
}

installDist.dependsOn 'war'

dependencies {
    compile project(':hexagon_store')
    compile project(':server_jetty')
    compile project(':templates_pebble')

    compile ("ch.qos.logback:logback-classic:$logbackVersion") { exclude module: 'slf4j-api' }
    compile ("org.mongodb:mongodb-driver:$mongodbVersion")
    compile ("com.zaxxer:HikariCP:$hikariVersion")
    compile ("org.postgresql:postgresql:$postgresqlVersion")

    testCompile ("org.testng:testng:$testngVersion")
    testCompile ("org.asynchttpclient:async-http-client:$ahcVersion") {
        exclude module: 'slf4j-api'
    }

    jmh ("org.openjdk.jmh:jmh-core:$jmhBenchmarkVersion")
    jmh ("org.openjdk.jmh:jmh-generator-annprocess:$jmhBenchmarkVersion")
}

test {
    useTestNG {
        parallel = "methods"
        threadCount = 4
    }
}

jmh {
    iterations = 5
    benchmarkMode = [ 'all' ]
    batchSize = 1
    fork = 1
    operationsPerInvocation = 5
    timeOnIteration = '1s'
    warmup = '1s'
    warmupBatchSize = 5
    warmupIterations = 1

    jmhVersion = jmhBenchmarkVersion
}

//noinspection GroovyAssignabilityCheck
task setupTfb(dependsOn: [ 'benchmarkCode', 'benchmarkConfig', 'setupScripts' ])

//noinspection GroovyAssignabilityCheck
task copyTfb(type: Copy) {
    from projectDir
    include 'src/**', 'tfb.gradle', 'readme.md'
    exclude 'src/jmh'
    into tfbBuildDir
    rename 'tfb.gradle', 'build.gradle'
}

//noinspection GroovyAssignabilityCheck
task gradleSettings(type: WriteProperties) {
    Map<String, ?> settings = [
        description : 'Hexagon web framework\'s benchmark',

        gradleScripts :
            "https://raw.githubusercontent.com/jaguililla/hexagon/$rootProject.version/gradle",

        hexagonVersion : rootProject.version,
        kotlinVersion : kotlinVersion,
        logbackVersion : logbackVersion,
        mongodbVersion : mongodbVersion,
        ahcVersion : ahcVersion,

        jettyVersion : jettyVersion,
        hikariVersion : hikariVersion,
        postgresqlVersion : postgresqlVersion,

        testngVersion : testngVersion
    ]

    properties settings
    outputFile = "$tfbBuildDir/gradle.properties"
}

import groovy.json.JsonOutput

//noinspection GroovyAssignabilityCheck
task benchmarkConfig() {
    doLast {
        Map<String, Map<String, Object>> test = [:]
        tests.forEach {
            String server = it[0]
            String database = it[1]
            String databaseCode = database == 'PostgreSQL'? 'postgres' : database.toLowerCase ()
            String name = server + '_' + database
            if (server == servers.first () && database == databases.first ())
                name = 'default'
            int port = server == 'Resin'? 8080 : 9090
            test[name.toLowerCase()] = [
                json_url : '/json',
                db_url : '/db',
                query_url : '/query?queries=',
                fortune_url : '/fortunes',
                update_url : '/update?queries=',
                plaintext_url : '/plaintext',

                port : port,

                approach : 'Realistic',
                classification : 'Micro',
                database : databaseCode,
                framework : 'Hexagon',
                language : 'Kotlin',
                orm : 'Raw',
                platform : 'Servlet',
                webserver : 'None',
                os : 'Linux',
                database_os : 'Linux',
                display_name : "Hexagon $server $database",
                notes : 'http://there4.co/hexagon',

                setup_file : "setup_${server.toLowerCase ()}_${database.toLowerCase ()}",
                versus : 'servlet'
            ]
        }

        Map<String, ?> config = [
            framework : 'hexagon',
            tests : [ test ]
        ]

        String json = JsonOutput.toJson(config)
        File file = file("$tfbBuildDir/benchmark_config.json")
        file.createNewFile()
        file.text = JsonOutput.prettyPrint(json)
    }
}

//noinspection GroovyAssignabilityCheck
task setupScripts() {
    doLast {
        tests.forEach {
            String server = ((String)it[0]).toLowerCase()
            String database = ((String)it[1]).toLowerCase()
            File file = file("$tfbBuildDir/setup_${server}_${database}.sh")
            file.createNewFile()
            String command = server == "resin"?
                """
                rm -rf \$RESIN_HOME/webapps/*
                cp build/libs/ROOT.war \$RESIN_HOME/webapps
                resinctl start
                """:
                'nohup build/install/hexagon/bin/hexagon &'
            file.text = """
                #!/bin/bash

                fw_depends java $database

                ./gradlew -x test
                export DBSTORE='$database'
                $command
            """.stripIndent ()
        }
    }
}

//noinspection GroovyAssignabilityCheck
task benchmarkCode(dependsOn: [ 'copyTfb', 'gradleSettings' ]) {
    doLast {
        List<String> extensions = [ 'kt', 'html', 'properties', 'xml', 'yaml', 'yml', 'gradle' ]
        String sources = fileTree (dir: tfbBuildDir, includes: extensions.collect { "**/*.$it" })
            .filter { it.file }
            .collect { './hexagon' + it.path.substring (tfbBuildDir.length()) }
            .sort ()
            .join ("\n")

        File file = file("$tfbBuildDir/source_code")
        file.createNewFile()
        file.text = sources
    }
}
