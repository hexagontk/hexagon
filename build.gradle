/*
 * For details on how to build and publish and release the project, check `contributing.md`.
 */

buildscript {
    dependencies {
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.14'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.1.4-2'
    id 'jacoco'

    id 'org.ajoberstar.git-publish' version '0.3.0' apply false
    id 'com.jfrog.bintray' version '1.7.3' apply false
    id 'org.xbib.gradle.plugin.jbake' version '1.2.1' apply false
    id 'uk.co.cacoethes.lazybones-templates' version '1.2.3' apply false
    id 'me.champeau.gradle.jmh' version '0.4.4' apply false
}

apply from: 'local.gradle'
apply from: 'gradle/release.gradle'

task publish(
    dependsOn: [
        ':hexagon_site:gitPublishPush',
        ':hexagon_starters:publishAllTemplates',

        ':hexagon_core:bintrayUpload',
        ':hexagon_rest:bintrayUpload',
        ':hexagon_scheduler:bintrayUpload',
        ':hexagon_store:bintrayUpload',
        ':hexagon_test:bintrayUpload',

        ':events_rabbitmq:bintrayUpload',
        ':server_servlet:bintrayUpload',
        ':server_jetty:bintrayUpload',
        ':server_undertow:bintrayUpload',
        ':templates_pebble:bintrayUpload'
    ]
)

repositories {
    jcenter ()
}

task jacocoReport(dependsOn: getTasksByName ('jacocoTestReport', true), type: JacocoReport) {
    executionData = files(fileTree(rootDir.absolutePath).include("**/build/jacoco/test.exec"))

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        html.enabled = true
        xml.enabled = true
    }
}
